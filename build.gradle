plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'com.fox2code'
version = '0.1.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    api 'org.ow2.asm:asm-commons:9.7'
}

jar {
    from "LICENSE"
}

shadowJar {
    relocate 'org.objectweb.asm', 'com.fox2code.jfallback.asm'
    manifest {
        attributes "Main-Class": "com.fox2code.jfallback.Main"
        attributes "Premain-Class": "com.fox2code.jfallback.Main"
        attributes "Launcher-Agent-Class": "com.fox2code.jfallback.Main"
    }
    from "LICENSE"
}

test {
    useJUnitPlatform()
}

assemble.dependsOn(shadowJar)

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.java
                groupId = "com.fox2code"
                artifactId = "JFallback"
                pom {
                    url = 'https://github.com/Fox2Code/JFallback'
                }
            }
        }
    }
}